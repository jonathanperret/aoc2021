~library/macros.tal

( devices )

|00 @System     [ &vector $2 &wst  $1 &rst    $1 &pad   $4 &r $2 &g $2 &b $2   &debug $1 &halt $1 ]
|10 @Console    [ &vector $2 &read $1 &pad    $5 &write $1 &error  $1 ]
|a0 @File       [ &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2 ]

( variables )

|0000
@best-turn   $2
@best-pick   $1
@best-score  $2

( init )

|0100 @program
  ;on-arg-char .Console/vector DEO2
  BRK !

@on-arg-char
  .Console/read DEI ( char )
  DUP #0a EQU ,&complete JCN
  ;argv #00 ,&argptr LDR ADD2 STA
  ,&argptr LDR INC ,&argptr STR
  BRK !

  &complete
  POP

  ;arg-received JMP2

  &argptr $1

@argv        $20

( arg is in @argv )
@arg-received
  LIT ': EMIT
  ;argv ;print JSR2
  LF

  ;open-file JSR2

  ;read-byte ;picks ;parse-byte-list JSR2
  ;picks ;dump-byte-list JSR2

  #ffff .best-turn STZ2

  &next-board
    ;eat-lf JSR2
    P< "-- >P LF
    ;read-byte ;board ;parse-board JSR2
    ;board ;dump-board JSR2

    ;board ;picks ;eval-board JSR2

    ( score pick turn )
    STH2k P< "turn= >P ;print-short-decimal JSR2 SP
    STHk P< "pick= >P #00 SWP ;print-short-decimal JSR2 SP
    STH2k P< "score= >P ;print-short-decimal JSR2 LF
    STH2r STHr STH2r
    ( score pick turn )

    DUP2 .best-turn LDZ2 GEQ2 ,&not-better JCN
      P< "new_best 0a >P
      ( score pick turn )
      .best-turn STZ2
      .best-pick STZ
      .best-score STZ2

      P< "result= >P
      #00 .best-pick LDZ .best-score LDZ2 MUL2 ;print-short-decimal JSR2 LF

      ;&next-board JMP2
    &not-better
    DUP2 ;print-short-decimal JSR2 SP P< "not_better_than >P SP .best-turn LDZ2 ;print-short-decimal JSR2 LF
    POP2 POP POP2
    ;&next-board JMP2

  !

@open-file
  ;argv .File/name DEO2
  #0001 .File/length DEO2
  RTN

( -- next-byte )
@read-byte
  #00 ;&byte STA ( clear byte so that EOF reads as zero )
  ;&byte .File/read DEO2
  LIT &byte 00 ( <- this byte is patched by the two previous instructions! )
  JMP2r

@eat-lf
  ;read-byte JSR2
  #0a NEQ ,&fail JCN
  RTN
  &fail
  BRK

~day4_lib.tal

@picks $ff
@board $19
@numbers
