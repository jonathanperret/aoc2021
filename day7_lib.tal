( parses 1,2,256\n into 0001 0002 0100 ffff )
( get-byte list -- )
@parse-short-list
  ;&list STA2
  ( patch call site )
  ;&get-byte STA2

  ,&first-number JMP

  &next-byte
    LIT2 [ &get-byte $2 ] JSR2 ( b )
    DUP #0a EQU ,&done JCN
    DUP #30 LTH ,&next-number JCN

    #01 [ ,&started STR ]
    #30 SUB #00 SWP
    STH2r #000a MUL2 ADD2 STH2
    ,&next-byte JMP

  &next-number
    ( separator ) POP ( )
    STH2r ,&append_if_started JSR
    &first-number
    #00 [ ,&started STR ]
    #0000 STH2 ( : num )
    ,&next-byte JMP

  &done
  ( 0a ) POP ( )

  ( append final number )
  STH2r ,&append_if_started JSR
  ( append terminator )
  #ffff ,&append JSR
  RTN

  ( num -- )
  &append_if_started
    [ ,&started LDR ] ,&append JCN
    ( num ) POP2 ( )
    RTN
  ( num -- )
  &append
    [ ,&list LDR2 ] STA2
    [ ,&list LDR2 ] INC2 INC2 [ ,&list STR2 ]
    RTN

  [ &list $2 &started $1 ]

( list -- )
@dump-short-list
  &loop
    DUP2 LDA2
    DUP2 #ffff EQU2 ,&done JCN
    ;print-short-decimal JSR2 SP
    INC2 INC2
    ,&loop JMP
  &done
  POP2 POP2
  LF
  RTN

( dest* src* -- )
@copy-short-list
  SWP2 STH2
  ( src* : dest* )
  &loop
    DUP2 LDA2
    DUP2 #ffff EQU2 ,&done JCN
    STH2rk STA2
    INC2r INC2r
    INC2 INC2
    ,&loop JMP
  &done
  ( terminator )
  STH2r STA2
  ( src* )
  POP2
  RTN

( list* stats* -- )
@list-stats
  STH2
  &loop
    DUP2 LDA
    DUP #ff EQU ,&done JCN
    #00 SWP 8** STH2rk ADD2
    #01 ;add64-byte JSR2

    INC2
    ,&loop JMP
  &done
  POP

  POP2 POP2r
  RTN
  &tmp $8

( stats* -- )
@dump-stats
  LITr 09
  &loop
    DUP2 ;print64 JSR2 LF
    #0008 ADD2
    DECr STHrk ,&loop JCN
  POP2 POPr
  RTN

( cost* from to -- )
@compute-cost
  GTH2k ,&pos JCN
    SWP2
    &pos
  SUB2
  ( cost* distance )

  STH2

  ;&result STH2kr ( cost* result* distance : distance )
  ;short-to-64 JSR2 ( cost* result=distance : distance )

  ( set tmp to distance )
  ;&tmp STH2r ( cost* tmp* distance ) ;short-to-64 JSR2

  ( cost* )

  ( tmp = tmp^2 )
  ;&tmp ;&tmp ;mul64 JSR2
  ;&tmp ;mul64/get-lo JSR2

  ( cost* )

  ( add tmp into result )
  ;&result ;&tmp ;add64 JSR2

  ( multiply result by 128 )
  ;&result #80 ;mul64-byte JSR2

  ( cost* )
  ( drop first byte of result (dividing by 256), copy into cost )
  ;&result INC2 ;copy64 JSR2

  RTN
  [ &result $9 &tmp $8 ]


( r* list* to -- )
@compute-list-cost
  STH2 ( r* list* : to )
  SWP2 [ ,&r STR2 ] ( list* : to )
  [ ,&r LDR2 ] #0000 ;short-to-64 JSR2
  ( list* : to )
  &loop
    DUP2 LDA2 ( list* from : to )
    DUP2 #ffff EQU2 ,&done JCN

    STH2rk ( list* from to : to )
    ;&cost ROT2 ROT2 ( list* cost* from to : to )
    ;compute-cost JSR2 ( list* : to )

    [ ,&r LDR2 ] ;&cost ;add64 JSR2

    INC2 INC2
    ,&loop JMP
  &done
  ( list* terminator : to )
  POP2 POP2
  POP2r
  RTN
  [ &r $2 &cost $8 ]

( list* tomin tomax -- )
@compute-list-cost-range
  ROT2 ( tomin tomax list* )
  STH2 SWP2 ( tomax tomin : list* )
  ( best = int64_max )
  #7f ;&best #0006 ADD2 STA
  ( tomax to : list* )
  &loop
    P< "-> 20 >P
    DUP2 DBGSHORTDECn POP2 SP
    P< ": >P
    ( tomax to : list* )
    ;&cost OVR2 ( tomax to cost* to : list* )
    STH2rk SWP2 ( tomax to cost* list* to : list* )
    ;compute-list-cost JSR2
    ;&cost ;print64 JSR2
    LF

    ;&tmp ;&cost ;&best ;sub64 JSR2
    ;&tmp ;is-positive64 JSR2
    NOT ,&new-best JCN
      ,&done JMP
    &new-best
    ;&best ;&cost ;copy64 JSR2

    INC2 ( tomax to+1 : list* )
    GEQ2k ,&loop JCN
  &done
  POP2 POP2 POP2r
  P< "best: >P
  ;&best ;print64 JSR2 LF

  RTN
  [ &cost $8 &best $8 &tmp $8 ]

~library/console.lib.tal
~library/string.tal
~library/math.tal
